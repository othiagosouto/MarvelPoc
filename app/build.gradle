apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'


android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["androidCompileSdkVersion"]
    buildToolsVersion globalConfiguration["androidBuildToolsVersion"]

    defaultConfig {
        applicationId "com.soutosss.marvelpoc"
        minSdkVersion globalConfiguration["androidMinSdkVersion"]
        targetSdkVersion globalConfiguration["androidTargetSdkVersion"]
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin/'
        test.java.srcDirs += 'src/test/kotlin/'
        androidTest.java.srcDirs += 'src/androidTest/kotlin/'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

def allTestsConfigs(dependency) {
    dependencies.add('testImplementation', dependency)
    dependencies.add('androidTestImplementation', dependency)
}

dependencies {
    def Dependencies = rootProject.ext.Dependencies
    def TestingDependencies = rootProject.ext.TestingDependencies

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(path: ':data')
    implementation project(path: ':shared')

    implementation Dependencies.kotlin
    implementation Dependencies.coroutines
    implementation Dependencies.androidxAppCompat
    implementation Dependencies.archx.lifecycleExtensions
    implementation Dependencies.design.androidxConstraintLayout
    implementation Dependencies.design.material

    implementation Dependencies.glide
    annotationProcessor Dependencies.compiler.glide

    implementation Dependencies.koin

    implementation Dependencies.ktx.core
    implementation Dependencies.ktx.viewModel
    implementation Dependencies.ktx.liveData

    testImplementation TestingDependencies.mockk

    allTestsConfigs TestingDependencies.gson
    allTestsConfigs TestingDependencies.androidxEspresso
    allTestsConfigs TestingDependencies.junit
    allTestsConfigs TestingDependencies.coroutines
    allTestsConfigs TestingDependencies.truth
    allTestsConfigs TestingDependencies.androidxTestRules
    allTestsConfigs TestingDependencies.androidxTestCore

    androidTestImplementation TestingDependencies.androidxTestRunner
    androidTestImplementation TestingDependencies.androidxTestJunit
    androidTestImplementation TestingDependencies.mockkAndroid

    debugImplementation TestingDependencies.fragment
}
