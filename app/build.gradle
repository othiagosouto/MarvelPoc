import dev.thiagosouto.Libs

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion Libs.Config.Android.androidCompileSdkVersion
    buildToolsVersion Libs.Config.Android.androidBuildToolsVersion

    defaultConfig {
        applicationId "com.soutosss.marvelpoc"
        minSdkVersion Libs.Config.Android.minSdkVersion
        targetSdkVersion Libs.Config.Android.targetSdkVersion
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin/'
        test.java.srcDirs += 'src/test/kotlin/'
        androidTest.java.srcDirs += 'src/androidTest/kotlin/'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion Libs.AndroidX.Compose.version
    }
}

def allTestsConfigs(dependency) {
    dependencies.add('testImplementation', dependency)
    dependencies.add('androidTestImplementation', dependency)
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':data-room')
    implementation project(path: ':data-retrofit')
    implementation project(path: ':data')
    implementation project(path: ':design')
    implementation project(path: ':shared')

    implementation Libs.Kotlin.stdlib
    implementation Libs.Coroutines.android
    implementation Libs.AndroidX.androidxAppCompat
    implementation Libs.AndroidX.lifecycleExtensions
    implementation Libs.AndroidX.androidxConstraintLayout
    implementation Libs.AndroidX.material

    implementation Libs.Network.glide
    annotationProcessor Libs.Network.glideCompiler

    implementation Libs.DI.koin

    implementation Libs.AndroidX.coreKtx
    implementation Libs.AndroidX.paging
    implementation Libs.AndroidX.viewModel
    implementation Libs.AndroidX.liveData

    //Compose
    implementation Libs.AndroidX.Compose.activityCompose
    implementation Libs.AndroidX.Compose.coil
    implementation Libs.AndroidX.Compose.runtimeLivedata
    implementation Libs.AndroidX.Compose.layout
    implementation Libs.AndroidX.Compose.material
    implementation Libs.AndroidX.Compose.toolingPreview
    implementation Libs.AndroidX.Compose.uiUtil
    implementation Libs.AndroidX.Compose.runtime
    implementation Libs.AndroidX.Compose.animation
    implementation Libs.AndroidX.Compose.graphics
    debugImplementation Libs.AndroidX.Compose.tooling


    testImplementation Libs.Test.mockk
    testImplementation Libs.AndroidX.Test.archRules
    testImplementation Libs.Coroutines.turbine

    allTestsConfigs Libs.Network.gson
    allTestsConfigs Libs.AndroidX.Test.espressoCore
    allTestsConfigs Libs.Test.junit
    allTestsConfigs Libs.Coroutines.test
    allTestsConfigs Libs.Test.truth
    allTestsConfigs Libs.AndroidX.Test.rules
    allTestsConfigs Libs.AndroidX.Test.core


    androidTestImplementation Libs.AndroidX.Compose.uiTest
    androidTestImplementation Libs.AndroidX.Test.runner
    androidTestImplementation Libs.AndroidX.Test.Ext.junit
    androidTestImplementation Libs.Test.mockkAndroid
    androidTestImplementation project(path: ':webserver')

    debugImplementation "androidx.fragment:fragment-testing:1.4.0"

    androidTestUtil "androidx.test:orchestrator:1.4.1"
}
