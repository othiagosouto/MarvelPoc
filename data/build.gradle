apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["androidCompileSdkVersion"]
    buildToolsVersion globalConfiguration["androidBuildToolsVersion"]

    defaultConfig {
        minSdkVersion globalConfiguration["androidMinSdkVersion"]
        targetSdkVersion globalConfiguration["androidTargetSdkVersion"]
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            useProguard false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "SERVICE_HOST", "\"https://gateway.marvel.com/v1/public/\"")
            buildConfigField("String", "DATABASE_NAME", "\"apps-database-prod\"")
        }
        debug {
            buildConfigField("String", "SERVICE_HOST", "\"https://gateway.marvel.com/v1/public/\"")
            buildConfigField("String", "DATABASE_NAME", "\"apps-database-debug\"")
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin/'
        test.java.srcDirs += 'src/test/kotlin/'
        androidTest.java.srcDirs += 'src/androidTest/kotlin/'
    }

}

def allTestsConfigs(dependency) {
    dependencies.add('testImplementation', dependency)
    dependencies.add('androidTestImplementation', dependency)
}

dependencies {

    def Dependencies = rootProject.ext.Dependencies
    def TestingDependencies = rootProject.ext.TestingDependencies

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':shared')

    implementation Dependencies.kotlin
    implementation Dependencies.coroutines
    implementation Dependencies.koin
    implementation Dependencies.retrofit
    implementation Dependencies.ktx.room
    kapt Dependencies.compiler.room

    implementation Dependencies.ktx.paging

    implementation Dependencies.gsonConverter

    allTestsConfigs TestingDependencies.junit
    testImplementation TestingDependencies.mockk
    allTestsConfigs TestingDependencies.coroutines
    allTestsConfigs TestingDependencies.truth

}
